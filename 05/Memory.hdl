// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

/*
address[15]
000000000000000 = 0       = RAM start
011111111111111 = 16,383  = RAM end
100000000000000 = 16,384  = Screen start
101111111111111 = 24,575  = Screen end
110000000000000 = 24,576  = Keyboard bit
*/

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    Not(in=address[14], out=shouldSelectRAM);
    And(a=load, b=shouldSelectRAM, out=shouldLoadRAM);
    RAM16K(in=in, load=shouldLoadRAM, address=address[0..13], out=outRAM);

    And(a=address[14], b=address[13], out=shouldSelectKeyboard);
    Keyboard(out=outKeyboard);

    Or(a=shouldSelectRAM, b=shouldSelectKeyboard, out=eitherRAMOrKeyboard);
    Not(in=eitherRAMOrKeyboard, out=shouldSelectScreen);
    And(a=load, b=shouldSelectScreen, out=shouldLoadScreen);
    Screen(in=in, load=shouldLoadScreen, address=address[0..12] , out=outScreen);

    Mux16(a=outScreen, b=outRAM, sel=shouldSelectRAM, out=outScreenOrRAM);
    Mux16(a=outScreenOrRAM, b=outKeyboard, sel=shouldSelectKeyboard, out=out);
}
